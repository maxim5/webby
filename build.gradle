import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'java'
    id 'java-library'
    id 'idea'
    id 'nu.studer.rocker' version '3.0.2'
}

group 'io.webby'
version '0.3.0-SNAPSHOT'

repositories {
    mavenCentral()
}

def os
if (Os.isFamily(Os.FAMILY_UNIX)) {
    os = 'linux64'
} else if (Os.isFamily(Os.FAMILY_MAC)) {
    os = 'osx64'
} else if (Os.isFamily(Os.FAMILY_WINDOWS)) {
    os = 'win64'
}

dependencies {
    // API
    api 'com.google.guava:guava:30.1.1-jre'
    api 'com.google.inject:guice:5.0.1'
    api 'com.google.mug:mug:5.5'
    api 'io.netty:netty-all:4.1.67.Final'

    // Implementation
    compileOnly 'com.google.protobuf:protobuf-java:3.17.3'
    implementation 'org.jetbrains:annotations:22.0.0'

    // RouteKit
    implementation 'io.routekit:routekit:v0.2.0'

    // Google Flogger
    compileOnly 'com.google.flogger:flogger:0.6'
    runtimeOnly 'com.google.flogger:flogger-log4j2-backend:0.6'

    // Json
    compileOnly 'com.alibaba:fastjson:1.2.78'
    compileOnly 'com.dslplatform:dsl-json-java8:1.9.8'
    compileOnly 'com.fasterxml.jackson.core:jackson-databind:2.12.4'
    compileOnly 'com.google.code.gson:gson:2.8.7'
    compileOnly 'com.squareup.moshi:moshi:1.12.0'

    // Templates
    compileOnly group: 'com.fizzed', name: 'rocker-runtime', version: '1.3.0'
    compileOnly group: 'com.github.jknack', name: 'handlebars', version: '4.2.0'
    compileOnly group: 'gg.jte', name: 'jte', version: '1.11.2'
    compileOnly group: 'io.pebbletemplates', name: 'pebble', version: '3.1.5'
    compileOnly group: 'org.freemarker', name: 'freemarker', version: '2.3.31'

    // Key-Values
    compileOnly 'com.linkedin.paldb:paldb:1.2.0'
    compileOnly 'io.swaydb:java_2.13:0.16.2'
    compileOnly 'net.openhft:chronicle-map:3.21ea81'
    compileOnly group: 'org.deephacks.lmdbjni', name: 'lmdbjni', version: '0.4.6'
    runtimeOnly group: 'org.deephacks.lmdbjni', name: "lmdbjni-$os", version: '0.4.6'
    compileOnly group: 'org.fusesource.leveldbjni', name: 'leveldbjni', version: '1.8'
    runtimeOnly group: 'org.fusesource.leveldbjni', name: "leveldbjni-$os", version: '1.8'
    compileOnly 'org.lmdbjava:lmdbjava:0.8.1'
    compileOnly 'org.mapdb:mapdb:3.0.8'
    compileOnly group: 'org.iq80.leveldb', name: 'leveldb', version: '0.12'
    compileOnly group: 'org.rocksdb', name: 'rocksdbjni', version: '6.22.1.1'

    // JUnit
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testCompileOnly 'org.junit.jupiter:junit-jupiter-params:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
    // Google Truth
    testImplementation 'com.google.truth:truth:1.1.3'
    // Use GSON for unit tests
    testImplementation 'com.google.code.gson:gson:2.8.7'
}

test {
    useJUnitPlatform()
}

apply from: 'src/examples/examples.gradle'
apply from: 'src/integration/integration.gradle'
